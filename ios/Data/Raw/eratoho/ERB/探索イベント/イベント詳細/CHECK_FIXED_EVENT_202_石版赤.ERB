;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_202
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値

LOCAL:3 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 70


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT


IF LOCAL:2 >= LOCAL:1 || FLAG:202 >= 100
	;経験値が入るのは最初だけ
	IF FLAG:202 < 100
		CALL ADD_ABL_EXP_PT(0, 800)
		FLAG:202 += 100
	ENDIF
	
	IF FLAG:201 < 200
		;石碑を読んでいない場合、詳細不明となる
		IF FLAG:AUTOMODE
		    PRINTFORML %CALLNAME:1%%TACHI()%発現了刻着不可思議的紋章的石板！
		    TWAIT FLAG:AUTOMODE * 400, 0
		ELSE
		    PRINTFORMW  %CALLNAME:1%%TACHI()%発現了刻着不可思議的紋章的石板！
		ENDIF

		PRINTFORML 但是調查了石板也沒有発現什麼特別的地方…
		CALL PRINT_MESSAGE(1, 200, 202, CALLNAME:1, "", 0)
		CALL PRINT_MESSAGE(FLAG:10, 200, 202, CALLNAME:1, "", 0)
		CALL PRINT_MESSAGE(FLAG:11, 200, 202, CALLNAME:1, "", 0)
	ELSE
		IF FLAG:AUTOMODE
		    PRINTFORML %CALLNAME:1%%TACHI()%発現了刻着不可思議的紋章的赤色石板！
		    TWAIT FLAG:AUTOMODE * 400, 0
		ELSE
		    PRINTFORMW  %CALLNAME:1%%TACHI()%発現了刻着不可思議的紋章的赤色石板！
		ENDIF

		IF FLAG:41
			PRINTFORML 但是這個石板已経在第{FLAG:41}順序吟唱過呪文了…
			CALL PRINT_MESSAGE(1, 200, 202, CALLNAME:1, "", 1)
			CALL PRINT_MESSAGE(FLAG:10, 200, 202, CALLNAME:1, "", 1)
			CALL PRINT_MESSAGE(FLAG:11, 200, 202, CALLNAME:1, "", 1)
		ELSE
			PRINTFORML 要在這個石板前面吟唱呪文麼？
			CALL ASK_YN
			IF RESULT == 0
				PRINTFORML %CALLNAME:1%%TACHI()%在石板前面吟唱了呪文！
				IF FLAG:42 && FLAG:43
					;他の２つ利用済み
					FLAG:41 = 3
				ELSEIF FLAG:42 || FLAG:43
					;他の１つ利用済み
					FLAG:41 = 2
				ELSE
					;ここが最初
					FLAG:41 = 1
				ENDIF
				
				IF FLAG:41 == 3
					;最後だった場合、光の渦出現
					CALL CHECK_PLATE_DATA
					IF RESULT == 0
						;ワープした場合、以後のイベント中断
						RETURN 2
					ELSE
						PRINTFORML %CALLNAME:1%%TACHI()%停在原地什麼也沒做、不久発光的漩渦消失了…
						CALL PRINT_MESSAGE(1, 200, 202, CALLNAME:1, "", 2)
						CALL PRINT_MESSAGE(FLAG:10, 200, 202, CALLNAME:1, "", 2)
						CALL PRINT_MESSAGE(FLAG:11, 200, 202, CALLNAME:1, "", 2)
					ENDIF
				ENDIF
			ELSE
				PRINTFORML %CALLNAME:1%%TACHI()%什麼也沒做從石板前面離開了…
				CALL PRINT_MESSAGE(1, 200, 202, CALLNAME:1, "", 3)
				CALL PRINT_MESSAGE(FLAG:10, 200, 202, CALLNAME:1, "", 3)
				CALL PRINT_MESSAGE(FLAG:11, 200, 202, CALLNAME:1, "", 3)
			ENDIF
		ENDIF
	ENDIF
	
	;どの場合でもイベントをその場に残す
	LOCAL:3 = 1
	IF FLAG:AUTOMODE
		TWAIT FLAG:AUTOMODE * 500, 0
	ELSE
		WAIT
	ENDIF
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

