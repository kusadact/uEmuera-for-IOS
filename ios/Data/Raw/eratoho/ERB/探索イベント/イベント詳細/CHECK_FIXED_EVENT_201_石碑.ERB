;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_201
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値

LOCAL:3 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 120


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT

;ドッペルモードだと必ず失敗する
IF FLAG:998
	;経験値が入るのは最初だけ
	IF FLAG:201 < 100
		CALL ADD_ABL_EXP_PT(0, 1200)
		FLAG:201 += 100
	ENDIF
	IF FLAG:AUTOMODE
	    PRINTFORML %CALLNAME:1%%TACHI()%発現了刻着不可思議文字的石碑！
	    TWAIT FLAG:AUTOMODE * 400, 0
	ELSE
	    PRINTFORMW  %CALLNAME:1%%TACHI()%発現了刻着不可思議文字的石碑！
	ENDIF

	PRINTFORML 但是%CALLNAME:1%%TACHI()%看不懂這個文字…
ELSEIF LOCAL:2 >= LOCAL:1 || FLAG:201 >= 100
	;経験値が入るのは最初だけ
	IF FLAG:201 < 100
		CALL ADD_ABL_EXP_PT(0, 1200)
		FLAG:201 += 100
	ENDIF
	
	IF FLAG:201 < 200 && (LOCAL:2 >= 190 || FLAG:207 == 0)
		;大成功か、辞書を持っていると石碑が読める
		FLAG:201 += 100
		FLAG:40 = RAND:6
	ENDIF
	IF FLAG:AUTOMODE
	    PRINTFORML %CALLNAME:1%%TACHI()%発現了刻着不可思議文字的石碑！
	    TWAIT FLAG:AUTOMODE * 400, 0
	ELSE
	    PRINTFORMW  %CALLNAME:1%%TACHI()%発現了刻着不可思議文字的石碑！
	ENDIF

	IF FLAG:201 < 200
		PRINTFORML 但是%CALLNAME:1%%TACHI()%看不懂這個文字…
		CALL PRINT_MESSAGE(1, 200, 201, CALLNAME:1, "", 0)
		CALL PRINT_MESSAGE(FLAG:10, 200, 201, CALLNAME:1, "", 0)
		CALL PRINT_MESSAGE(FLAG:11, 200, 201, CALLNAME:1, "", 0)
	ELSE
		IF FLAG:207 == 0
			PRINTFORML 使用以前揀到的辞書、%CALLNAME:1%%TACHI()%解読了刻在石碑上的文章
		ELSE
			PRINTFORML 注意到了石碑上的文字是用統一言語記述的%CALLNAME:1%%TACHI()%、想辦法解読了内容
		ENDIF
		PRINTFORML 看来是要找到和這個石碑刻有同樣紋章的赤・青・黄的石板、
		PRINTFORML 按照特定的順序向那些石板吟唱呪文、好像就可以從這裡逃出去了
		;★EXモードではどの順番で読んでもおっけーね。さすがに順不同じゃないと辛い
		IF FLAG:999
			PRINTFORML 只要顔色斉了無論用什麼順序都沒有問題……
		ELSE
			PRINTFORML 但是很遺憾的是石碑上記載着選択石板的順序的部分因為残缺無法解読出来…
		ENDIF
		CALL PRINT_MESSAGE(1, 200, 201, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:10, 200, 201, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:11, 200, 201, CALLNAME:1, "", 1)
	ENDIF
	
	;どの場合でもイベントをその場に残す
	LOCAL:3 = 1
	IF FLAG:AUTOMODE
		TWAIT FLAG:AUTOMODE * 500, 0
	ELSE
		WAIT
	ENDIF
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

