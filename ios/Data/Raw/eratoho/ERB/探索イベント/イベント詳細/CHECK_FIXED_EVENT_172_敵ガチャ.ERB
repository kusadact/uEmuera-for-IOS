;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_172
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値

;必ず発見する系のイベントなのでもうGET_SEARCH_RATEいいよね……
;経験値が入るのは最初だけ
PRINTFORML
IF FLAG:172 < 100
	CALL ADD_ABL_EXP_PT(0, 300)
	FLAG:172 += 100
	PRINTFORML 碰到了奇怪的部屋
	PRINTFORML 在墻壁上有個大窟窿……
	PRINTFORML 想着発生了什麼靠近一看、洞裡面立着一塊看板
	PRINTFORML
	PRINTFORML 「がちゃがちゃ　こーなー」
	PRINTFORML 「おだいは　じょうかりょく」
	PRINTFORML
	
	IF FLAG:AUTOMODE
		PRINTFORML 太可疑了……
		TWAIT FLAG:AUTOMODE * 1000 , 0
	ELSE
		PRINTFORMW 太可疑了……
	ENDIF
	
ELSE
	PRINTFORML 「がちゃがちゃ　こーなー」
	PRINTFORML 「おだいは　じょうかりょく」
	
	IF FLAG:AUTOMODE
		TWAIT FLAG:AUTOMODE * 1000 , 0
	ELSE
		PRINTFORMW 和上次看到的東西沒有任何変化……
	ENDIF
	
ENDIF
PRINTFORML 要怎麼辦？
PRINTFORML [ 1] - 試試看 （必要浄化力:50）
PRINTFORML [ 0] - 還是算了
$INPUT_LOOP

	IF FLAG:AUTOMODE
		TINPUT FLAG:AUTOMODE * 1000 , 0, 0, ""
	ELSE
		INPUT
	ENDIF
IF RESULT == 0
	PRINTFORML 不想和這麼可疑的東西搭上関係……
	IF FLAG:AUTOMODE
		PRINTFORML 決定離開這裡
		TWAIT FLAG:AUTOMODE * 1000 , 0
	ELSE
		PRINTFORMW 決定離開這裡
	ENDIF
	RETURN
ELSEIF RESULT == 1 && FLAG:6 < 50
	PRINTFORML 但是浄化力不足……
	PRINTFORMW 沒辦法、這次只好放棄了
	RETURN
ELSEIF RESULT != 1
	GOTO INPUT_LOOP
ENDIF

FLAG:6 -= 50

SIF FLAG:172 < 200
	FLAG:172 += 100

IF RAND:10
	CALL REPOP_SELECT(0)
	PRINTFORML 雖然顕然很可疑、但還是輸給了好奇心使用了浄化力……
	PRINTFORML 於是、敵人從洞中出現、襲撃了過来！
	PRINTFORMW 扭蛋機的奨品好像就是這些玩意……！
	IF FLAG:98
		PRINTFORML 但是、被結界阻擋而無法靠近……
		RETURN 1
	ENDIF
	CALL PRE_BATTLE(4)
	RETURN 2
ELSE
	;こっちパターンはイベント削除
	FLAG:171 = 0
	PRINTFORML 雖然顕然很可疑、但還是輸給了好奇心使用了浄化力……
	PRINTFORML 但是、不小心注入了過多浄化力！
	PRINTFORML 結果如何了、從扭蛋機的深処傳来了駭人的悲鳴……
	
	
	IF (FLAG:512 & 1) == 1
		PRINTFORML 看来、好像浄化掉了潜藏在洞穴深処的触手
		PRINTFORML 過了一会、從扭蛋機的洞中跑出了很多小人們
		PRINTFORML 看来是他們、把触手送到了洞穴裡面
		PRINTFORML 他們看到了%CALLNAME:1%%TACHI()%、因為棲息地被弄得乱七八糟的而憤怒的襲撃了過来！
		PRINTFORMW 
		CALL CREATE_ENEMY(61, FLAG:5, 0)
		CALL CREATE_ENEMY(61, FLAG:5, 0)
		CALL CREATE_ENEMY(61, FLAG:5, 0)
		CALL CREATE_ENEMY(61, FLAG:5, 0)
		CALL CREATE_ENEMY(61, FLAG:5, 0)
		IF FLAG:98
			PRINTFORML 但是、被結界阻擋而無法靠近……
			RETURN 1
		ENDIF
		CALL PRE_BATTLE(4)
		RETURN 2
	ELSE
		PRINTFORML 從裡面傳来了什麼東西坏掉的声音和什麼東西転動的声音……
		PRINTFORML 然後就什麼反応都沒有了
		;何も起こらないのもあれなのでランク2のわきフラグ5匹分増加
		FLAG:657 += 500
	ENDIF
ENDIF

RETURN 0