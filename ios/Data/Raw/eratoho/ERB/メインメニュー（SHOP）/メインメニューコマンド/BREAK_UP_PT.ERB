;仲間と別れる
@BREAK_UP_PT
;LOCAL
;0	LOOP

IF FLAG:10 == 0
	PRINTFORMW 沒有其他同伴
	RETURN
ENDIF

PRINTFORML 要和誰分別？

SIF FLAG:10
	PRINTFORML [1] %CALLNAME:(FLAG:10)%
SIF FLAG:11
	PRINTFORML [2] %CALLNAME:(FLAG:11)%
PRINTFORML [0] 取消

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1 && FLAG:10
	CALL BREAK_UP_TALK(FLAG:10)
	IF RESULT
		CALL MEMBER_EXIT(0)
	ELSE
		RESTART
	ENDIF
ELSEIF RESULT == 2 && FLAG:11
	CALL BREAK_UP_TALK(FLAG:11)
	IF RESULT
		CALL MEMBER_EXIT(1)
	ELSE
		RESTART
	ENDIF
ELSE
	REUSELASTLINE 輸入値不正確。請重新輸入
	GOTO INPUT_LOOP
ENDIF


;離脱会話
;ARG:0	対象キャラ
@BREAK_UP_TALK(ARG:0)

CALL PRINT_MESSAGE(ARG:0, 110, ARG:0, CALLNAME:(ARG:0), "", 0)
PRINTFORML 真的確定要和%CALLNAME:(ARG:0)%分開麼？
CALL ASK_YN
SIF RESULT
	RETURN 0

CALL PRINT_MESSAGE(ARG:0, 110, ARG:0, CALLNAME:(ARG:0), "", 1)
CALL PRINT_MESSAGE(1, 110, ARG:0, CALLNAME:(ARG:0), "", 1)

IF CFLAG:(ARG:0):29
	;嫌がるだけの気力もない
ELSEIF CFLAG:(ARG:0):5 < 200
	;特に嫌がられない
ELSE
	;好感度ダウン
	;形見とは言わないけど、何か渡そうかなぁ
	CFLAG:(ARG:0):5 -= 50
ENDIF


RETURN 1



;離脱チェック
;ARG:0	対象キャラ
@BREAK_UP_CHECK(ARG:0)

SIF ARG:0 == 0
	RETURN

IF CFLAG:(ARG:0):5 < 0 && CHECK_FINE(ARG:0)
	;行動可能で好感度が舞ナスの場合、離脱
	CALL PRINT_MESSAGE(ARG:0, 161, ARG:0, CALLNAME:(ARG:0), "")
	FOR LOCAL:0, 0, 3
		;残される側の口上表示
		SIF GET_MEMBER(LOCAL:0) != ARG:0
			CALL PRINT_MESSAGE(GET_MEMBER(LOCAL:0), 162, ARG:0, CALLNAME:(ARG:0), "")
	NEXT
	CALL MEMBER_EXIT_ID(ARG:0)
ENDIF


