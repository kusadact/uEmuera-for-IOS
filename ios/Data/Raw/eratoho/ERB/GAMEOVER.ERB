;ゲームオーバー
;クリア時もここに処理をまとめる
;ARG:0	
@GAMEOVER(ARG:0)

;クイーン撃破
IF ARG:0 == 0
	IF CHARANUM <= 2
		;完全ソロ撃破
		CALL PRINT_MESSAGE(1, 212, 0, CALLNAME:1)
		FLAG:214 = 0
	ELSE
		LOCAL:1 = 0
		FOR LOCAL:0, 2, CHARANUM
			IF CFLAG:(LOCAL:0):2 == 0 && CFLAG:(LOCAL:0):3 == 0
				LOCAL:1 = 1
				BREAK
			ENDIF
		NEXT
		
		IF LOCAL:1
			;普通に撃破
			CALL PRINT_MESSAGE(1, 210, 0, CALLNAME:1)
			IF FLAG:214 == 0
				CALL PRINT_MESSAGE(FLAG:10, 211, 0, CALLNAME:1)
				CALL PRINT_MESSAGE(FLAG:11, 211, 0, CALLNAME:1)
			ELSEIF FLAG:214 >= 1
				DA:2:1 = 0
				DA:3:1 = 0
				RETURN
			ENDIF
		ELSE
			;ソロ撃破
			CALL PRINT_MESSAGE(1, 212, 0, CALLNAME:1)
			FLAG:214 = 0
		ENDIF
	ENDIF
	CALL HISTORY(0)
ENDIF

;ガーディアン撃破
IF ARG:0 == 1
	CALL PRINT_MESSAGE(1, 206, 0, CALLNAME:1)
	;門番撃破後パッチ、選択肢によって続行可能
	IF FLAG:213 == 0
		CALL PRINT_MESSAGE(FLAG:10, 207, 0, CALLNAME:1)
		CALL PRINT_MESSAGE(FLAG:11, 207, 0, CALLNAME:1)
		CALL HISTORY(1)
	ELSEIF FLAG:213 == 1 || FLAG:213 == 2
		RETURN
	ENDIF
ENDIF

;ガーディアンに味方２人が捕まった状態で逃げると囮エンド
IF ARG:0 == 2
	CALL PRINT_MESSAGE(1, 204, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:10, 205, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:11, 205, 0, CALLNAME:1)
	CALL HISTORY(2)
ENDIF


;石版エンド
IF ARG:0 == 3
	CALL PRINT_MESSAGE(1, 208, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:10, 209, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:11, 209, 0, CALLNAME:1)
	CALL HISTORY(3)
ENDIF

;敗北エンド
IF ARG:0 == 4
	LOCAL:1 = CFLAG:1:103
	CALL PRINT_MESSAGE(1, 157, LOCAL:1, CALLNAME:1, ENEMY_NAME(LOCAL:1))
	CALL HISTORY(4)
ENDIF

;淫らでニューゲーム
;クイーンやガーディアン撃破をした場合エンドレスモードがあるがそれとは別
$INPUT_LOOP
PRINTFORML 
PRINTFORML 要保留開発度直接從最初開始麼？
PRINTFORML [ 0] 是
PRINTFORML [ 1] 否
PRINTFORML [ 2] 只改变主人公后从头来过
PRINTFORML [99] 确认角色的能力
PRINTFORML 


INPUT
IF RESULT == 0
	FLAG:3 += 1
	FLAG:49 = 0
	CALL NEWGAME
ELSEIF RESULT == 1
	RESETDATA
	BEGIN TITLE
ELSEIF RESULT == 2
	CALL CHARA_SELECT
	FLAG:96 = RESULT
	CALL NEWGAME
ELSEIF RESULT == 99
	FLAG:49 = ARG:0 + 1
	CALL SHOW_FINALCHARADATA
	GOTO INPUT_LOOP
ELSE
	CLEARLINE 1
	REUSELASTLINE 輸入値不正確。請重新輸入
	GOTO INPUT_LOOP
ENDIF



@SHOW_FINALCHARADATA
#DIM LCOUNT


	$INPUT_LOOP_2
	DRAWLINE
	FOR LCOUNT, 1, MIN(CHARANUM,30)
		IF CFLAG:(LCOUNT):4 != 59
			PRINTFORM [%@"{LCOUNT}",3,RIGHT%] %CALLNAME:LCOUNT,14,LEFT% 
			
			PRINTFORM 現在在{CFLAG:(LCOUNT):4}号房间
				IF LCOUNT == 1 || LCOUNT == FLAG:10 || LCOUNT == FLAG:11
					IF FLAG:49 == 1
						PRINTFORML 打败了女王逃出了
					ELSEIF FLAG:49 == 2
						PRINTFORML 打败了守卫逃出了
					ELSEIF FLAG:49 == 3
						IF LCOUNT == 1
							PRINTFORML 打败了守卫逃出了
						ELSEIF LCOUNT == FLAG:10 || LCOUNT == FLAG:11
							PRINTFORML %CALLNAME:1%被当作了逃脱的诱饵…
						ENDIF
					ELSEIF FLAG:49 == 4
						PRINTFORML 解开了石板之谜逃出了
					ELSEIF FLAG:49 == 5
						IF LCOUNT == 1
							PRINTFORML 被%ENEMY_NAME(CFLAG:1:103)%打败…
						ELSEIF LCOUNT == FLAG:10 || LCOUNT == FLAG:11
							PRINTFORML 和%CALLNAME:1%一起被%ENEMY_NAME(CFLAG:1:103)%打败…
						ENDIF
					ELSE
						PRINTFORML 徘徊着
					ENDIF
				ELSEIF CFLAG:(LCOUNT):2
					IF CFLAG:(LCOUNT):58
						PRINTFORML 被%ENEMY_NAME(CFLAG:(LCOUNT):103)%杀害、变成了不会说话的尸体…
					;悪堕ち転生
					ELSEIF TALENT:(LCOUNT):73>0
						IF TALENT:(LCOUNT):73==101
							PRINTFORML 屈服、向迷宫之主宣誓了忠诚…
						;「〇〇化」だとエネミー名が不自然なので決め打ち
						ELSEIF TALENT:(LCOUNT):73==103
							PRINTFORML 敗北、重生为曼陀罗花精…
						ELSEIF TALENT:(LCOUNT):73==104
							PRINTFORML 敗北、重生为史莱姆女孩…
						ELSEIF TALENT:(LCOUNT):73==105
							PRINTFORML 敗北、重生为拉米亚…
						ELSE
							PRINTFORML 完全敗北、重生为了%ENEMY_NAME(TALENT:(LCOUNT):73)%…
						ENDIF
					;通常ロスト
					ELSE
						PRINTFORML 败给了%ENEMY_NAME(CFLAG:(LCOUNT):103)%、变成了优质的苗床
					ENDIF
				ELSEIF CFLAG:(LCOUNT):10
					PRINTFORML 被%ENEMY_NAME(CFLAG:(LCOUNT):10)%捕获
				ELSEIF CFLAG:(LCOUNT):29
					PRINTFORML 丧失了斗志
				ELSE
					PRINTFORML 徘徊着
				ENDIF
		ENDIF
	NEXT
	PRINTL
	PRINTL [ 0] 离开

	INPUT
	
	IF RESULT == 0
		REDRAW 1
		RETURN
	ELSEIF 1 <= RESULT && RESULT < MIN(CHARANUM,30)
		LOCAL:1 = RESULT
		PRINTFORML {LOCAL:1}
		CALL CHECK_STATUS(LOCAL:1)
		GOTO INPUT_LOOP_2
	ELSE
		REUSELASTLINE 输入不正确！
		CLEARLINE 1
		GOTO INPUT_LOOP_2
	ENDIF

