
; ------------------------------------------------------------------------------
; ■ 160 MAP表示時 - ROOT
; ------------------------------------------------------------------------------

@KOJO_27_0_SYSTEM_MESSAGE_160(ARG:0, ARG:1, ARGS:0, ARGS:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6)
#DIM FUNCTION_COUNT
	;初期化
	VARSET FUNCTION_COUNT
	
	; 喪失中なら何も喋らずに戻る
	SIF CFLAG:29
		RETURN 0
	
	; りばーす東方触手宮では「131 眠気が進行」「160 MAP表示時」が連続して表示される
	; 違和感があるので片方だけ表示するよう調整
	; りばーす東方触手宮プレイ 兼 無印触手宮モードで口上が動作している時のみ
	IF KOJO_27_0_IS_REVERSE() && CFLAG:811 == 1
		SIF KOJO_27_0_GET_PREV_ARG(0) == 131 && GROUPMATCH(KOJO_27_0_GET_PREV_ARG(1), 3, 7, 11, 15)
			RETURN 0
	ENDIF
	
	; MAP表示時口上を一括で読み込み
	WHILE (1)
		CALL KOJO_27_0_DEPLOY_MESSAGE_SINGLE(ARG:0, ARG:1, ARGS:0, ARGS:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, @"KOJO_27_0_SYSTEM_MESSAGE_160_MAPTALK_{FUNCTION_COUNT}")
		IF RESULT:1
			FUNCTION_COUNT++
		ELSE
			; 呼び出し先関数無し、全部のメッセージは読み込んだ
			BREAK
		ENDIF

		; 読み込み中に優先口上があったら抜ける
		SIF KOJO_27_0_GET_IMPORTANT_FLAG()
			RETURN 1
	WEND
RETURN 1
