;非消耗武器一覧表示
@SHOW_WEAPON_LIST

REDRAW 0
DRAWLINE
PRINTFORML 現在所持的非消耗武器如下
DRAWLINE

FOR LOCAL:0, 10, 18
	IF ITEM:(LOCAL:0)
		;0番のキャラを利用して表示を行う
		CFLAG:0:7 = LOCAL:0
		PRINTFORM %ITEMNAME:(LOCAL:0), 20, LEFT% {ITEM:(LOCAL:0)}個 
		CALL ITEM_EXPLAIN(0, 0)
		PRINTL  
	ELSE
		SETCOLOR 0x333333
		PRINTFORML －－－
		RESETCOLOR
	ENDIF
NEXT
REDRAW 1

;非消耗武器選択
;RETURN	装備するアイテムの番号
@SELECT_WEAPON(ARGS:0)

DRAWLINE
IF ARGS:0 == ""
	PRINTFORML 請選択要装備的道具
ELSE
	PRINTFORML %ARGS:0%
ENDIF
DRAWLINE

FOR LOCAL:0, 1, 8
	IF ITEM:(LOCAL:0 + 9)
		LOCAL:(LOCAL:0) = 1
		;0番のキャラを利用して表示を行う
		CFLAG:0:7 = LOCAL:0 + 9
		PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0 + 9), 20, LEFT% {ITEM:(LOCAL:0 + 9)}個 
		CALL ITEM_EXPLAIN(0, 0)
		PRINTL  
	ELSE
		LOCAL:(LOCAL:0) = 0
		SETCOLOR 0x333333
		PRINTFORML [{LOCAL:0}] －－－
		RESETCOLOR
	ENDIF
NEXT
PRINTFORML [0] 返回

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF (RESULT >= 1) && (RESULT <= 7) && (LOCAL:(RESULT) == 1)
	RETURN (RESULT + 9)
ELSE
	REUSELASTLINE 輸入値不正確。請重新輸入
	GOTO INPUT_LOOP
ENDIF

RESTART



